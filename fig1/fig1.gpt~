reset
set auto
set term epslatex size 5,3 color colortext 16 font ",14" standalone

set output "fig1.tex"

############################                                                                                                                                                                                                                
#ColorBlind friendly palette                                                                                                                                                                                                               
############################   
colorSet(name, rgb) = sprintf("color_%s = %d", name, rgb)
colorGet(name) = value(sprintf("color_%s", name))                                                                                                                                                                                           
 
eval colorSet("c1", 0x999999)                                                                                                                                                                                                               
eval colorSet("c2", 0xe69f00)                                                                                                                                                                                                               
eval colorSet("c3", 0x56b4e9)                                                                                                                                                                                                               
eval colorSet("c4", 0x009e73)                                                                                                                                                                                                               
eval colorSet("c5", 0xcc79a7)                                                                                                                                                                                                               
eval colorSet("c6", 0x0072b2)                                                                                                                                                                                                               
eval colorSet("c7", 0xd55e00)                                                                                                                                                                                                               
eval colorSet("c8", 0xf0e442)  

TOP=0.97
BOTTOM=0.135
RIGHT=0.999
LEFT=0.15

set rmargin at screen RIGHT
set lmargin at screen LEFT
set tmargin at screen TOP
set bmargin at screen BOTTOM

set xlabel "$\\xi$" offset 0,0.9
set ylabel "$\\xi^{T_1T_2}_{1,2}/\\xi$" offset 1.6,0
set key bottom left
set xtics offset 0,0.4
set ytics offset 0.7,-0.05

p [2.9:9.1] "data_T0709.txt" u 1:($2/$1):($3/$1) w e  pt -1 lc rgb colorGet("c2") lw 6 t "$T_1 = 0.7$ ; $T_2=0.9$",\
"data_T0711.txt" u 1:($2/$1):($3/$1) w e lc rgb colorGet("c3") pt -1 lw 6 t "$T_1 = 0.7$ ; $T_2=1.1$"

